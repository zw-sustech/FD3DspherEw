!******************************************************************************!
!*  This program inits the media of the 3D geology model                      *!
!*                                                                            *!
!*  Author: Wei ZHANG     Email: zhangw.pku@gmail.com                         *!
!*  Copyright (C) Wei ZHANG, 2006. All Rights Reserved.                       *!
!******************************************************************************!

! $Date$
! $Revision$
! $LastChangedBy$

#include "mod_macdrp.h"
!-----------------------------------------------------------------------------
program seis3d_media
!-----------------------------------------------------------------------------

use constants_mod
use string_mod, only : string_conf
use math_mod
use para_mod
use mpi_mod
use nfseis_mod
use grid_mod
use media_mod
use custom_mod
use io_mod

implicit none

integer,parameter :: NGRID=4
integer,parameter :: NGRIDX=1
integer,parameter :: NGRIDY=1
integer,parameter :: NGRIDZ=4

integer,dimension(SEIS_GEO) ::         &
     subs,subc,subt,                   &
     subs_x1,subc_x1, subs_x2,subc_x2, &
     subs_y1,subc_y1, subs_y2,subc_y2, &
     subs_z1,subc_z1, subs_z2,subc_z2
integer,dimension(LenFD) :: indx_x1,indx_x2,indx_y1,indx_y2,indx_z1,indx_z2
integer :: i,j,k,n_i,n_j,n_k
character (len=SEIS_STRLEN) :: filenm
logical :: flagnc,flaglayer

real(SP) Vp,dtmax,dtlocal
integer,dimension(SEIS_GEO) :: dtnode,dtindx
real(SP) dtLe,dtmaxVp,dtmaxL

!-----------------------------------------------------------------------------

!fnm_media_conf='SeisMedia.conf'

call get_conf_name(fnm_conf)
call para_init(fnm_conf)
call swmpi_init(fnm_conf)
call grid_fnm_init(fnm_conf)
call media_fnm_init(fnm_conf)
call media_alloc
call grid_alloc

print *, 'init media ...'
! generate nc file
do n_i=0,dims(1)-1
do n_j=0,dims(2)-1
do n_k=0,dims(3)-1
   write(*,"(i10,2(i2),a,3(i2))") n_i,n_j,n_k, ' of ',dims
   call swmpi_change_fnm(n_i,n_j,n_k)
   call swmpi_set_gindx(n_i,n_j,n_k)

   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_data_create(filenm,nx,ny,nz, &
                       "media generated by seis3d_media" )
   subs=(/ nx1,ny1,nz1 /); subc=(/ nx,ny,nz /); subt=(/ 1,1,1 /)
   call nfseis_data_attput(filenm,        &
        subs,subc,subt,                   &
        ni1,ni2,nj1,nj2,nk1,nk2,ni,nj,nk, &
        nx1,nx2,ny1,ny2,nz1,nz2,nx,ny,nz, &
        ngi1,ngi2,ngj1,ngj2,ngk1,ngk2,    &
        ngx1,ngx2,ngy1,ngy2,ngz1,ngz2,    &
        (/ngi1,ngi2,ngj1,ngj2,ngk1,ngk2/) )
   call nfseis_data_addvar(filenm,'rho')
   call nfseis_data_addvar(filenm,'lambda')
   call nfseis_data_addvar(filenm,'mu')
#ifdef WITHQS
   call nfseis_data_addvar(filenm,'Qs')
#endif
end do
end do
end do

! media from nc
call read_media_nc(fnm_media_conf)

! media by layered
call read_media_layered(fnm_media_conf)

! media by mod_custom.f90
call read_media_custom(fnm_media_conf)

! exchange
subs_x1=(/ ni2+1,ny1  ,nz1  /); subc_x1=(/ LenFD , ny    , nz    /)
subs_x2=(/ nx1  ,ny1  ,nz1  /); subc_x2=(/ LenFD , ny    , nz    /)
subs_y1=(/ nx1  ,nj2+1,nz1  /); subc_y1=(/ nx    , LenFD , nz    /)
subs_y2=(/ nx1  ,ny1  ,nz1  /); subc_y2=(/ nx    , LenFD , nz    /)
subs_z1=(/ nx1  ,ny1  ,nk2+1/); subc_z1=(/ nx    , ny    , LenFD /)
subs_z2=(/ nx1  ,ny1  ,nz1  /); subc_z2=(/ nx    , ny    , LenFD /)
indx_x1=(/ (i,i=ni1,ni1+LenFD-1) /)
indx_x2=(/ (i,i=ni2-LenFD+1,ni2) /)
indx_y1=(/ (j,j=nj1,nj1+LenFD-1) /)
indx_y2=(/ (j,j=nj2-LenFD+1,nj2) /)
indx_z1=(/ (k,k=nk1,nk1+LenFD-1) /)
indx_z2=(/ (k,k=nk2-LenFD+1,nk2) /)
subs=(/ nx1,ny1,nz1 /); subc=(/ nx,ny,nz /); subt=(/ 1,1,1 /)

dtmax=1.0e10

write(*,*) "exchange media on boundary stencil ..."

do n_i=0,dims(1)-1
do n_j=0,dims(2)-1
do n_k=0,dims(3)-1
   write(*,"(i10,2(i2),a,3(i2))") n_i,n_j,n_k, ' of ',dims
   call swmpi_change_fnm(n_i,n_j,n_k)
   call swmpi_set_gindx(n_i,n_j,n_k)
   call media_import
! check
   call grid_import
   do k=nk1,nk2
   do j=nj1,nj2
   do i=ni1,ni2
      Vp=sqrt( (lambda(i,j,k)+2.0*mu(i,j,k))/rho(i,j,k) )
      dtLe=min( stepx*xsin(i)*z(k),stepy*z(k),stepz)
      dtlocal=1.3/Vp * dtLe 
      if (dtlocal<dtmax) then
         dtmax=dtlocal
         dtnode=(/ n_i,n_j,n_k /)
         dtindx=(/ i,j,k /)
         dtmaxVp=Vp
         dtmaxL=dtLe
      end if
   end do
   end do
   end do
   ! to x1
if (n_i>0) then
   call swmpi_change_fnm(n_i-1,n_j,n_k)
   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_varput(filenm,'rho',rho(indx_x1,:,:),               &
        subs_x1,subc_x1,subt)
   call nfseis_varput(filenm,'mu'      ,mu(indx_x1,:,:)      ,     &
        subs_x1,subc_x1,subt)
   call nfseis_varput(filenm,'lambda'  ,lambda(indx_x1,:,:)  ,     &
        subs_x1,subc_x1,subt)
end if
! to x2
if (n_i<dims(1)-1) then
   call swmpi_change_fnm(n_i+1,n_j,n_k)
   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_varput(filenm,'rho', rho(indx_x2,:,:),              &
        subs_x2,subc_x2,subt)
   call nfseis_varput(filenm,'mu', mu(indx_x2,:,:),                &
        subs_x2,subc_x2,subt)
   call nfseis_varput(filenm,'lambda', lambda(indx_x2,:,:),        &
        subs_x2,subc_x2,subt)
end if
! to y1
if (n_j>0) then
   call swmpi_change_fnm(n_i,n_j-1,n_k)
   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_varput(filenm,'rho', rho(:,indx_y1,:),              &
        subs_y1,subc_y1,subt)
   call nfseis_varput(filenm,'mu', mu(:,indx_y1,:),                &
        subs_y1,subc_y1,subt)
   call nfseis_varput(filenm,'lambda', lambda(:,indx_y1,:),        &
        subs_y1,subc_y1,subt)
end if
! to y2
if (n_j<dims(2)-1) then
   call swmpi_change_fnm(n_i,n_j+1,n_k)
   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_varput(filenm,'rho', rho(:,indx_y2,:),              &
        subs_y2,subc_y2,subt)
   call nfseis_varput(filenm,'mu', mu(:,indx_y2,:),                &
        subs_y2,subc_y2,subt)
   call nfseis_varput(filenm,'lambda', lambda(:,indx_y2,:),        &
        subs_y2,subc_y2,subt)
end if
! to k1
if (n_k>0) then
   call swmpi_change_fnm(n_i,n_j,n_k-1)
   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_varput(filenm,'rho', rho(:,:,indx_z1),              &
        subs_z1,subc_z1,subt)
   call nfseis_varput(filenm,'mu', mu(:,:,indx_z1),                &
        subs_z1,subc_z1,subt)
   call nfseis_varput(filenm,'lambda', lambda(:,:,indx_z1),        &
        subs_z1,subc_z1,subt)
end if
! to k2
if (n_k<dims(3)-1) then
   call swmpi_change_fnm(n_i,n_j,n_k+1)
   filenm=swmpi_rename_fnm(pnm_media,fnm_media)
   call nfseis_varput(filenm,'rho', rho(:,:,indx_z2),              &
        subs_z2,subc_z2,subt)
   call nfseis_varput(filenm,'mu', mu(:,:,indx_z2),                &
        subs_z2,subc_z2,subt)
   call nfseis_varput(filenm,'lambda', lambda(:,:,indx_z2),        &
        subs_z2,subc_z2,subt)
end if
end do
end do
end do

print *, "Maximum allowed time step is", dtmax
write(*,"(a,3i5,a,3i5)") "located on", dtindx,' in thread', dtnode
print *, " Vp and dL are:", dtmaxVp,dtmaxL
if (dtmax<stept) then
   print *, "Serious Error: stept>dtmax", stept,dtmax
   stop 1
end if

call media_destroy
call grid_dealloc

!-----------------------------------------------------------
contains
!-----------------------------------------------------------

subroutine read_media_nc(fnm_conf)
character (len=*) :: fnm_conf

integer,dimension(SEIS_GEO) :: ijknc,subsnc,subcnc
integer,dimension(SEIS_GEO) :: subs,subc,subt
character (len=SEIS_STRLEN) ::  fnm_list_nc,filenm

integer fid
integer k,n,gk,k1,k2
integer n1,n2,n3
integer n_i,n_j,n_k
real(SP),dimension(:,:),allocatable :: grho,glam,gmu
#ifdef WITHQS
real(SP),dimension(:,:),allocatable :: gQs
real(SP) Qf0
#endif

fid=1001
! read parameters
open(fid,file=trim(fnm_conf),status="old")
call string_conf(fid,1,'fnm_list_nc',2,fnm_list_nc)
do n=1,SEIS_GEO
   call string_conf(fid,1,'I1J1K1_in_nc',n+1,ijknc(n))
end do
#ifdef WITHQS
call string_conf(fid,1,'Qf0',2, Qf0)
#endif
close(fid)

! return if nc isn't needed
flagnc=.false.
if (any(ijknc==0)) return
flagnc=.true.

! allocate
n1=swmpi_globi(nx2,dims(1)-1)
n2=swmpi_globj(ny2,dims(2)-1)
n3=swmpi_globk(nz2,dims(3)-1)
allocate(grho(nx1:n1,ny1:n2))
allocate(glam(nx1:n1,ny1:n2))
allocate(gmu(nx1:n1,ny1:n2))
#ifdef WITHQS
allocate(gQs(nx1:n1,ny1:n2))
#endif

! parameters
n1=swmpi_globi(ni2,dims(1)-1)
n2=swmpi_globj(nj2,dims(2)-1)
n3=swmpi_globk(nk2,dims(3)-1)

! loop gk
do gk=nk1,n3
   subsnc=(/ijknc(1),ijknc(2),ijknc(3)+(gk-nk1)/)
   subcnc=(/ni*dims(1),nj*dims(2),1/)
! read from nc
call read_nc_list('./',fnm_list_nc,'rho',    &
     grho(ni1:n1,nj1:n2),subsnc,subcnc)
call read_nc_list('./',fnm_list_nc,'lambda', &
     glam(ni1:n1,nj1:n2),subsnc,subcnc)
call read_nc_list('./',fnm_list_nc,'mu',     &
     gmu(ni1:n1,nj1:n2),subsnc,subcnc)
#ifdef WITHQS
call read_nc_list('./',fnm_list_nc,'Q',      &
     gQs(ni1:n1,nj1:n2),subsnc,subcnc)
     gQs(ni1:n1,nj1:n2)=exp((-PI*Qf0*stept)/gQs(ni1:n1,nj1:n2))
#endif

! check
if (     any(grho(ni1:n1,nj1:n2)<=0.0) &
    .or. any(glam(ni1:n1,nj1:n2)<=0.0) &
    .or. any(gmu (ni1:n1,nj1:n2)<=0.0)) then
   print *, 'read_media from nc error, parameter zero'
   print *, gk
   stop 1
end if

! put to nc
   n_k=(gk-nk1)/nk
   k=swmpi_loclk(gk,n_k)
do n=1,LenFD
   grho(ni1-n,:)=grho(ni1,:); grho(ni2+n,:)=grho(ni2,:)
   glam(ni1-n,:)=glam(ni1,:); glam(ni2+n,:)=glam(ni2,:)
   gmu (ni1-n,:)=gmu (ni1,:); gmu (ni2+n,:)=gmu (ni2,:)
#ifdef WITHQS
   gQs(ni1-n,:)=gQs(ni1,:); gQs(ni2+n,:)=gQs(ni2,:)
#endif
end do
if (n_k==0 .and. k==nk1) then
   k1=nz1; k2=nk1
elseif (n_k==dims(3)-1 .and. k==nk2) then
   k1=nk2; k2=nz2
else
   k1=k; k2=k
end if
do k=k1,k2
do n_i=0,dims(1)-1
do n_j=0,dims(2)-1
   call swmpi_change_fnm(n_i,n_j,n_k)
   call swmpi_set_gindx(n_i,n_j,n_k)
   filenm=swmpi_rename_fnm(pnm_grid,fnm_metric)
   subs=(/ nx1,ny1,k /); subc=(/ nx,ny,1 /); subt=(/ 1,1,1 /)
   call nfseis_varput(filenm,'rho',grho(ngx1:ngx2,ngy1:ngy1),subs,subc,subt)
   call nfseis_varput(filenm,'lambda',glam(ngx1:ngx2,ngy1:ngy1),subs,subc,subt)
   call nfseis_varput(filenm,'mu',gmu(ngx1:ngx2,ngy1:ngy1),subs,subc,subt)
#ifdef WITHQS
   call nfseis_varput(filenm,'Qs',gQs(ngx1:ngx2,ngy1:ngy1),subs,subc,subt)
#endif
end do
end do !n_i
end do

end do !gk
deallocate(grho,glam,gmu)
#ifdef WITHQS
deallocate(gQs)
#endif
end subroutine read_media_nc

subroutine read_media_layered(fnm_conf)
character (len=*) :: fnm_conf

!integer,parameter :: NXGRID=5,NYGRID=5,NZGRID=10

real(SP),dimension(:,:),allocatable ::        &
   dom_vp, dom_vs, dom_p, dom_u, dom_x, dom_Q
real,dimension(2) ::                          &
   lam, miu, Qatt,                            &
   Vp,Vs,dens
real(SP),allocatable ::                       &
   x_layer(:),y_layer(:),z_layer(:,:,:),      &
   vecz(:)
real(SP) x1,y1,z1,L1,L2
integer imax,jmax,nlayer,i1vec(1),j1vec(1)

character (len=SEIS_STRLEN) :: str
real(SP) d2m,v2ms,d2kgm3
real(SP) Qf0

integer fid
integer :: i,j,k,n,i1,i2,j1,j2,mi,mj,mk
integer n_i,n_j,n_k

flaglayer=.false.
if (flagnc) return
flaglayer=.true.

fid=1001
open(fid,file=trim(fnm_conf),status="old")
call string_conf(fid,1,'nlayer',2,nlayer)
if (nlayer<1) then
   flaglayer=.false.
   close(fid)
   return
end if
call string_conf(fid,1,'imax',2,imax)
call string_conf(fid,1,'jmax',2,jmax)
call string_conf(fid,1,'distance2meter',2,d2m)
call string_conf(fid,1,'velocity2m/s',2,v2ms)
call string_conf(fid,1,'density2kg/m^3',2,d2kgm3)
allocate (dom_vs(2,nlayer))
allocate (dom_vp(2,nlayer))
allocate (dom_p (2,nlayer))
allocate (dom_u (2,nlayer))
allocate (dom_x (2,nlayer))
allocate (dom_Q (2,nlayer))
allocate (x_layer(imax))
allocate (y_layer(jmax))
allocate (z_layer(imax,jmax,nlayer))
allocate (vecz (nlayer))

do n=1,nlayer
   call string_conf(fid,1,'vp',2*n  ,dom_vp(1,n))
   call string_conf(fid,1,'vp',2*n+1,dom_vp(2,n))
   call string_conf(fid,1,'vs',2*n  ,dom_vs(1,n))
   call string_conf(fid,1,'vs',2*n+1,dom_vs(2,n))
   call string_conf(fid,1,'rho',2*n  ,dom_p(1,n))
   call string_conf(fid,1,'rho',2*n+1,dom_p(2,n))
   call string_conf(fid,1,'Q',2*n  ,  dom_Q(1,n))
   call string_conf(fid,1,'Q',2*n+1,  dom_Q(2,n))
end do
call string_conf(fid,1,'Qf0',2, Qf0)
dom_vp(1,1)=dom_vp(2,1);dom_vs(1,1)=dom_vs(2,1)
dom_p(1,1)=dom_p(2,1);dom_Q(1,1)=dom_Q(2,1)

dom_vp=dom_vp*v2ms
dom_vs=dom_vs*v2ms
dom_p=dom_p*d2kgm3

! check
if ( any(dom_p<=0) .or. any(dom_vp<=0) .or. any(dom_vs<=0) ) then
   print *, 'media parameter is negtive'
   print *, 'dom_vp=',dom_vp
   print *, 'dom_vs=',dom_vs
   print *, 'dom_p=',dom_p
   stop 1
end if
if ( any(dom_vp**2<=2.0*dom_vs**2) ) then
   print *, 'Vp**2 < 2Vs**2'
   print *, 'dom_vp=',dom_vp
   print *, 'dom_vs=',dom_vs
   stop 1
end if

call string_conf(fid,1,'<Layer',2,str)
do j=1,jmax
do i=1,imax     
    read(fid,*) x_layer(i),y_layer(j),( z_layer(i,j,n), n=1,nlayer )
end do 
end do
do n=1,nlayer-1
   if (any(z_layer(:,:,n)<z_layer(:,:,n+1))) then
      print *, 'layer',n,' should be beyond layer',n+1
      print *, minloc(z_layer(:,:,n)-z_layer(:,:,n+1))
      stop 1
   end if
end do
!x_layer=x_layer*d2m; y_layer=y_layer*d2m;
x_layer=x_layer*PI/180.0_SP
y_layer=y_layer*PI/180.0_SP
z_layer=z_layer*d2m
z_layer=z_layer+coord0(3)
close(fid)

write(*,*) "get media through media_layered ..."

! model
do n_i=0,dims(1)-1
do n_j=0,dims(2)-1
do n_k=0,dims(3)-1

  write(*,"(i10,2(i2),a,3(i2))") n_i,n_j,n_k, ' of ',dims
  call swmpi_change_fnm(n_i,n_j,n_k)
  call swmpi_set_gindx(n_i,n_j,n_k)
  call grid_import

do k=nk1,nk2
do j=nj1,nj2
do i=ni1,ni2

   rho   (i,j,k)=0.0
   lambda(i,j,k)=0.0
   mu    (i,j,k)=0.0

do mk=-NGRIDZ,NGRIDZ
do mj=-NGRIDY,NGRIDY
do mi=-NGRIDX,NGRIDX
   if (mi*mj*mk==0) cycle
   x1=x(i)+(mi*0.5_SP/(NGRIDX+1.0_SP))*stepx
   y1=y(j)+(mj*0.5_SP/(NGRIDY+1.0_SP))*stepy
   z1=z(k)+(mk*0.5_SP/(NGRIDZ+1.0_SP))*stepz

   x1=min(max(x1,x_layer(1)),x_layer(imax))
   y1=min(max(y1,y_layer(1)),y_layer(jmax))
   i1vec=minloc(x_layer,x_layer-x1+SEIS_EQUAL>=0.0);i2=max(i1vec(1),2);i1=i2-1
   j1vec=minloc(y_layer,y_layer-y1+SEIS_EQUAL>=0.0);j2=max(j1vec(1),2);j1=j2-1
   do n=1,nlayer
      vecz(n)=interp_2d(x_layer(i1:i2),y_layer(j1:j2), &
                         z_layer(i1:i2,j1:j2,n),       &
                         2,2,x1,y1)
   end do

if (z1>vecz(1) .or. abs(z1-vecz(1))<=SEIS_EQUAL) then
   n=1
   Vp(1)=dom_vp(1,n); Vp(2)=dom_vp(1,n)
   Vs(1)=dom_vs(1,n); Vs(2)=dom_vs(1,n)
   dens(1)=dom_p(1,n); dens(2)=dom_p(1,n)
#ifdef WITHQS
   Qatt(1)=dom_Q(1,n); Qatt(2)=dom_Q(1,n)
#endif
elseif (abs(z1-vecz(nlayer))<=SEIS_EQUAL) then
   n=nlayer
   Vp(1)=dom_vp(1,n); Vp(2)=dom_vp(2,n)
   Vs(1)=dom_vs(1,n); Vs(2)=dom_vs(2,n)
   dens(1)=dom_p(1,n); dens(2)=dom_p(2,n)
#ifdef WITHQS
   Qatt(1)=dom_Q(1,n); Qatt(2)=dom_Q(2,n)
#endif
elseif (z1<vecz(nlayer)) then
   n=nlayer
   Vp(1)=dom_vp(2,n); Vp(2)=dom_vp(2,n)
   Vs(1)=dom_vs(2,n); Vs(2)=dom_vs(2,n)
   dens(1)=dom_p(2,n); dens(2)=dom_p(2,n)
#ifdef WITHQS
   Qatt(1)=dom_Q(2,n); Qatt(2)=dom_Q(2,n)
#endif
else
   loop_lay_n: do n=nlayer-1,1,-1
   if (abs(z1-vecz(n))<=SEIS_EQUAL) then
      Vp(1)=dom_vp(1,n); Vp(2)=dom_vp(2,n)
      Vs(1)=dom_vs(1,n); Vs(2)=dom_vs(2,n)
      dens(1)=dom_p(1,n); dens(2)=dom_p(2,n)
#ifdef WITHQS
      Qatt(1)=dom_Q(1,n); Qatt(2)=dom_Q(2,n)
#endif
      exit loop_lay_n
   elseif (vecz(n)>z1) then
      L1=(z1-vecz(n+1))/(vecz(n)-vecz(n+1))
      L2=(z1-vecz(n))/(vecz(n+1)-vecz(n))
      Vp(:)=dom_vp(2,n)*L1+dom_vp(1,n+1)*L2
      Vs(:)=dom_vs(2,n)*L1+dom_vs(1,n+1)*L2
      dens(:)=dom_p(2,n)*L1+dom_p(1,n+1)*L2
#ifdef WITHQS
      Qatt(:)=dom_Q(2,n)*L1+dom_Q(1,n+1)*L2
#endif
      exit loop_lay_n
   end if
   end do loop_lay_n
end if

   miu=dens*Vs*Vs
   lam=Vp*Vp*dens - 2.0*miu

   !accumulate
      rho(i,j,k)=rho   (i,j,k)+0.5*(dens(1)+dens(2))
       mu(i,j,k)=mu    (i,j,k)+0.5*(1.0/miu(1)+1.0/miu(2))
   lambda(i,j,k)=lambda(i,j,k)+0.5*(1.0/lam(1)+1.0/lam(2))
#ifdef WITHQS
   Qs(i,j,k)=Qs(i,j,k)+0.5(Qatt(1)+Qatt(2))
#endif
end do !mi
end do !mj
end do !mk

    rho   (i,j,k)=rho(i,j,k)/(2*NGRIDX*2*NGRIDY*2*NGRIDZ)
    mu    (i,j,k)=(2*NGRIDX*2*NGRIDY*2*NGRIDZ)/mu(i,j,k)
    lambda(i,j,k)=(2*NGRIDX*2*NGRIDY*2*NGRIDZ)/lambda(i,j,k)
#ifdef WITHQS
    Qs(i,j,k)=Qs(i,j,k)/(2*NGRIDX*2*NGRIDY*2*NGRIDZ)
    Qs(i,j,k)=exp((-PI*Qf0*stept)/Qs(i,j,k))
#endif

end do !i
end do !j
end do !k

  call media_extend
  filenm=swmpi_rename_fnm(pnm_media,fnm_media)
  call nfseis_varput(filenm,'rho',rho,subs,subc,subt)
  call nfseis_varput(filenm,'mu',mu,subs,subc,subt)
  call nfseis_varput(filenm,'lambda',lambda,subs,subc,subt)
#ifdef WITHQS
  call nfseis_varput(filenm,'Qs',Qs,subs,subc,subt)
#endif

end do
end do
end do

  if (allocated(dom_vp)) deallocate(dom_vp)
  if (allocated(dom_vs)) deallocate(dom_vs)
  if (allocated(dom_p)) deallocate(dom_p)
  if (allocated(dom_u)) deallocate(dom_u)
  if (allocated(dom_x)) deallocate(dom_x)
  if (allocated(dom_Q)) deallocate(dom_Q)
  if (allocated(x_layer)) deallocate(x_layer)
  if (allocated(y_layer)) deallocate(y_layer)
  if (allocated(z_layer)) deallocate(z_layer)
  if (allocated(vecz)) deallocate(vecz)
end subroutine read_media_layered

subroutine read_media_custom(fnm_conf)
character (len=*) :: fnm_conf

real,dimension(2) ::  &
   lam, miu, Qatt,    &
   Vp,Vs,dens
real x1,y1,z1

real Qf0

integer fid
integer :: i,j,k,mi,mj,mk,si,sj,sk
integer n_i,n_j,n_k
character (len=SEIS_STRLEN) :: model_name
real dx1,dx2,dy1,dy2,dz1,dz2

if (flagnc .or. flaglayer) return

fid=1001
open(fid,file=trim(fnm_conf),status="old")
call string_conf(fid,1,'model_custom',2,model_name)
close(fid)

write(*,*) "get media through media_custom ..."

! model
do n_i=0,dims(1)-1
do n_j=0,dims(2)-1
do n_k=0,dims(3)-1

  write(*,"(i10,2(i2),a,3(i2))") n_i,n_j,n_k, ' of ',dims
  call swmpi_change_fnm(n_i,n_j,n_k)
  call swmpi_set_gindx(n_i,n_j,n_k)
  call grid_import

do k=nk1,nk2
do j=nj1,nj2
do i=ni1,ni2

   rho   (i,j,k)=0.0
   lambda(i,j,k)=0.0
   mu    (i,j,k)=0.0

do mk=-NGRID,NGRID
do mj=-NGRID,NGRID
do mi=-NGRID,NGRID
   if (mi*mj*mk==0) cycle
   x1=x(i)+(mi-0.5_SP*sign(1,mi))*stepx
   y1=y(j)+(mj-0.5_SP*sign(1,mj))*stepy
   z1=z(k)+(mk-0.5_SP*sign(1,mk))*stepz

   call custom_media(x1,y1,z1,Vp,Vs,dens,Qatt,Qf0,model_name)
   miu=dens*Vs*Vs
   lam=Vp*Vp*dens - 2.0*miu
   !accumulate
   rho(i,j,k)=rho(i,j,k)+0.5*(dens(1)+dens(2))
    mu(i,j,k)=mu(i,j,k)+0.5*(1.0/miu(1)+1.0/miu(2))
   lambda(i,j,k)=lambda(i,j,k)+0.5*(1.0/lam(1)+1.0/lam(2))
#ifdef WITHQS
   Qs(i,j,k)=Qs(i,j,k)+0.5(Qatt(1)+Qatt(2))
#endif
end do !mi
end do !mj
end do !mk
    rho   (i,j,k)=rho(i,j,k)/((2*NGRID)**3)
    mu    (i,j,k)=((2*NGRID)**3)/mu(i,j,k)
    lambda(i,j,k)=((2*NGRID)**3)/lambda(i,j,k)
#ifdef WITHQS
    Qs(i,j,k)=Qs(i,j,k)/((2*NGRID)**3)
    Qs(i,j,k)=exp((-PI*Qf0*stept)/Qs(i,j,k))
#endif
end do !i
end do !j
end do !k

  call media_extend
  filenm=swmpi_rename_fnm(pnm_media,fnm_media)
  call nfseis_varput(filenm,'rho',rho,subs,subc,subt)
  call nfseis_varput(filenm,'mu',mu,subs,subc,subt)
  call nfseis_varput(filenm,'lambda',lambda,subs,subc,subt)
#ifdef WITHQS
  call nfseis_varput(filenm,'Qs',Qs,subs,subc,subt)
#endif

end do
end do
end do
end subroutine read_media_custom

subroutine media_extend
  call extend_equal(rho)
  call extend_equal(mu)
  call extend_equal(lambda)
#ifdef WITHQS
  call extend_equal(Qs)
#endif
end subroutine media_extend
subroutine extend_equal(w)
   real(SP),dimension(nx1:nx2,ny1:ny2,nz1:nz2) :: w
   integer i,j,k,n
   ! x1, x2
   do k=nz1,nz2
   do j=ny1,ny2
      do n=1,LenFD
         w(ni1-n,j,k)=w(ni1,j,k)
         w(ni2+n,j,k)=w(ni2,j,k)
      end do
   end do
   end do
   ! y1, y2
   do k=nz1,nz2
   do i=nx1,nx2
      do n=1,LenFD
         w(i,nj1-n,k)=w(i,nj1,k)
         w(i,nj2+n,k)=w(i,nj2,k)
      end do
   end do
   end do
   ! z1, z2
   do j=ny1,ny2
   do i=nx1,nx2
      do n=1,LenFD
         w(i,j,nk1-n)=w(i,j,nk1)
         w(i,j,nk2+n)=w(i,j,nk2)
      end do
   end do
   end do
end subroutine extend_equal
function dist_point2plane(x0,y0,z0,A,B,C) result (L)
real(SP) x0,y0,z0
real(SP),dimension(SEIS_GEO) :: A,B,C
real(SP) L
real(SP),dimension(SEIS_GEO) :: AB,AC,p
real(SP) c1,c2,c3,d

AB=B-A; AC=C-A
call times_product(AB,AC,p)
c1=p(1);c2=p(2);c3=p(3);
d=-dot_product(p,A)
L=abs( (c1*x0+c2*y0+c3*z0+d)/sqrt(dot_product(p,p)) )
end function dist_point2plane

end program seis3d_media

